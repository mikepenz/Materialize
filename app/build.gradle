apply plugin: 'com.android.application'
//wrap with try and catch so the build is working even if the signing stuff is missing
try {
    apply from: '../../../signing.gradle'
} catch (ex) {
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 101
        versionName '1.0.1'

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def file = output.outputFile
                def fileName = file.name.replace(".apk", "-v" + versionName + "-c" + versionCode + ".apk")
                output.outputFile = new File(file.parentFile, fileName)
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            try {
                signingConfig signingConfigs.debug
            } catch (ex) {
            }
            minifyEnabled false
        }
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ex) {
            }
            zipAlignEnabled true
            minifyEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile "com.android.support:cardview-v7:${rootProject.ext.supportLibVersion}"

    compile project(':library')

    //used to generate the drawer on the left
    //https://github.com/mikepenz/MaterialDrawer
    compile('com.mikepenz:materialdrawer:5.8.1@aar') {
        transitive = true
        exclude group: 'com.android.support'
        exclude group: 'com.mikepenz', module: 'materialize'
    }

    // used to generate the Open Source section
    // https://github.com/mikepenz/AboutLibraries
    compile('com.mikepenz:aboutlibraries:5.9.3@aar') {
        transitive = true
        exclude group: 'com.android.support'
    }
}
